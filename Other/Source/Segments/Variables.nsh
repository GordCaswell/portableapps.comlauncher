${SegmentFile}

; Macros {{{1
!macro ParseLocations_SlashType VAR SLASHTYPE VARIABLEAPPENDAGE ;{{{2
	${StrReplace} "${VAR}" "%${SLASHTYPE}APPDIR%" "$${VARIABLEAPPENDAGE}APPDIRECTORY" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}DATADIR%" "$${VARIABLEAPPENDAGE}DATADIRECTORY" "${VAR}"
	${If} $JavaMode == find
	${OrIf} $JavaMode == require
		${StrReplace} "${VAR}" "%${SLASHTYPE}JAVADIR%" "$${VARIABLEAPPENDAGE}JAVADIRECTORY" "${VAR}"
	${EndIf}
	${StrReplace} "${VAR}" "%${SLASHTYPE}ALLUSERSPROFILE%" "$${VARIABLEAPPENDAGE}ALLUSERSPROFILE" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}LOCALAPPDATA%" "$${VARIABLEAPPENDAGE}LOCALAPPDATA" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}APPDATA%" "$${VARIABLEAPPENDAGE}APPDATA" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}DOCUMENTS%" "$${VARIABLEAPPENDAGE}DOCUMENTS" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}TEMP%" "$${VARIABLEAPPENDAGE}TEMPDIRECTORY" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSDOCUMENTSDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSDOCUMENTSDIRECTORY" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSPICTURESDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSPICTURESDIRECTORY" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSMUSICDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSMUSICDIRECTORY" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSVIDEOSDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSVIDEOSDIRECTORY" "${VAR}"
	${StrReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSDIRECTORY" "${VAR}"
!macroend

!macro ParseLocations VAR ;{{{2
	${DebugMsg} "Before location parsing, $${VAR} = `${VAR}`"
	;===Paths {{{3
		${StrReplace} "${VAR}" %DRIVE% $CurrentDrive "${VAR}"
		!insertmacro ParseLocations_SlashType "${VAR}" "" ""
		!insertmacro ParseLocations_SlashType "${VAR}" / REPLACEVAR_FS_
		!insertmacro ParseLocations_SlashType "${VAR}" \\ REPLACEVAR_DBS_
		${If} $JavaMode == find
		${OrIf} $JavaMode == require
			!insertmacro ParseLocations_SlashType "${VAR}" java.util.prefs: REPLACEVAR_JUP_
		${EndIf}

	;===Languages {{{3
		${StrReplace} "${VAR}" %LANGCODE% $PORTABLEAPPSLANGUAGECODE "${VAR}"
		${StrReplace} "${VAR}" %LANGCODE2% $PORTABLEAPPSLOCALECODE2 "${VAR}"
		${StrReplace} "${VAR}" %LANGCODE3% $PORTABLEAPPSLOCALECODE3 "${VAR}"
		${StrReplace} "${VAR}" %LANGGLIBC% $PORTABLEAPPSLOCALEGLIBC "${VAR}"
		${StrReplace} "${VAR}" %LANGID% $PORTABLEAPPSLOCALEID "${VAR}"
		${StrReplace} "${VAR}" %LANGWINNAME% $PORTABLEAPPSLOCALEWINNAME "${VAR}"
	${DebugMsg} "After location parsing, $${VAR} = `${VAR}`"
!macroend
!define ParseLocations "!insertmacro ParseLocations"

; Variables {{{1
Var APPDIRECTORY
Var DATADIRECTORY
Var ALLUSERSPROFILE
Var TEMPDIRECTORY
Var PORTABLEAPPSDOCUMENTSDIRECTORY
Var PORTABLEAPPSPICTURESDIRECTORY
Var PORTABLEAPPSMUSICDIRECTORY
Var PORTABLEAPPSVIDEOSDIRECTORY
Var PORTABLEAPPSDIRECTORY

!macro Var_ReplaceVar _VAR
Var REPLACEVAR_FS_${_VAR}
Var REPLACEVAR_DBS_${_VAR}
Var REPLACEVAR_JUP_${_VAR}
!macroend

!insertmacro Var_ReplaceVar APPDIRECTORY
!insertmacro Var_ReplaceVar DATADIRECTORY
!insertmacro Var_ReplaceVar ALLUSERSPROFILE
!insertmacro Var_ReplaceVar LOCALAPPDATA
!insertmacro Var_ReplaceVar APPDATA
!insertmacro Var_ReplaceVar DOCUMENTS
!insertmacro Var_ReplaceVar TEMPDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSDOCUMENTSDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSPICTURESDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSMUSICDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSVIDEOSDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSDIRECTORY

Var PORTABLEAPPSLANGUAGECODE
Var PORTABLEAPPSLOCALECODE2
Var PORTABLEAPPSLOCALECODE3
Var PORTABLEAPPSLOCALEGLIBC
Var PORTABLEAPPSLOCALEID
Var PORTABLEAPPSLOCALEWINNAME

; Segments {{{1
${SegmentInit}
	;=== Initialise variables
	${GetParent} $EXEDIR $PORTABLEAPPSDIRECTORY

	ReadEnvStr $PORTABLEAPPSDOCUMENTSDIRECTORY PortableApps.comDocuments
	${IfNotThen} ${FileExists} $PORTABLEAPPSDOCUMENTSDIRECTORY ${|} StrCpy $PORTABLEAPPSDOCUMENTSDIRECTORY $CurrentDrive\Documents ${|}

	ReadEnvStr $PORTABLEAPPSPICTURESDIRECTORY PortableApps.comPictures
	${IfNotThen} ${FileExists} $PORTABLEAPPSPICTURESDIRECTORY ${|} StrCpy $PORTABLEAPPSPICTURESDIRECTORY $PORTABLEAPPSDOCUMENTSDIRECTORY\Pictures ${|}

	ReadEnvStr $PORTABLEAPPSMUSICDIRECTORY PortableApps.comMusic
	${IfNotThen} ${FileExists} $PORTABLEAPPSMUSICDIRECTORY ${|} StrCpy $PORTABLEAPPSMUSICDIRECTORY $PORTABLEAPPSDOCUMENTSDIRECTORY\Music ${|}

	ReadEnvStr $PORTABLEAPPSVIDEOSDIRECTORY PortableApps.comVideos
	${IfNotThen} ${FileExists} $PORTABLEAPPSVIDEOSDIRECTORY ${|} StrCpy $PORTABLEAPPSVIDEOSDIRECTORY $PORTABLEAPPSDOCUMENTSDIRECTORY\Videos ${|}

	ReadEnvStr $PORTABLEAPPSLANGUAGECODE PortableApps.comLanguageCode
	${IfThen} $PORTABLEAPPSLANGUAGECODE == "" ${|} StrCpy $PORTABLEAPPSLANGUAGECODE en-us ${|}
	ReadEnvStr $PORTABLEAPPSLOCALECODE2 PortableApps.comLocaleCode2
	${IfThen} $PORTABLEAPPSLOCALECODE2 == "" ${|} StrCpy $PORTABLEAPPSLOCALECODE2 en ${|}
	ReadEnvStr $PORTABLEAPPSLOCALECODE3 PortableApps.comLocaleCode3
	${IfThen} $PORTABLEAPPSLOCALECODE3 == "" ${|} StrCpy $PORTABLEAPPSLOCALECODE3 eng ${|}
	ReadEnvStr $PORTABLEAPPSLOCALEGLIBC PortableApps.comLocaleglibc
	${IfThen} $PORTABLEAPPSLOCALEGLIBC == "" ${|} StrCpy $PORTABLEAPPSLOCALEGLIBC en_US ${|}
	ReadEnvStr $PORTABLEAPPSLOCALEID PortableApps.comLocaleID
	${IfThen} $PORTABLEAPPSLOCALEID == "" ${|} StrCpy $PORTABLEAPPSLOCALEID 1033 ${|}
	ReadEnvStr $PORTABLEAPPSLOCALEWINNAME PortableApps.comLocaleWinName
	${IfThen} $PORTABLEAPPSLOCALEWINNAME == "" ${|} StrCpy $PORTABLEAPPSLOCALEWINNAME LANG_ENGLISH ${|}

	ReadEnvStr $ALLUSERSPROFILE ALLUSERSPROFILE

	;=== Make forward slash and double backslash versions
	${StrReplace} $REPLACEVAR_FS_ALLUSERSPROFILE \ / $ALLUSERSPROFILE
	${StrReplace} $REPLACEVAR_DBS_ALLUSERSPROFILE / \\ $REPLACEVAR_FS_ALLUSERSPROFILE
	${StrReplace} $REPLACEVAR_FS_LOCALAPPDATA \ / $LOCALAPPDATA
	${StrReplace} $REPLACEVAR_DBS_LOCALAPPDATA / \\ $REPLACEVAR_FS_LOCALAPPDATA
	${StrReplace} $REPLACEVAR_FS_APPDATA \ / $APPDATA
	${StrReplace} $REPLACEVAR_DBS_APPDATA / \\ $REPLACEVAR_FS_APPDATA
	${StrReplace} $REPLACEVAR_FS_DOCUMENTS \ / $DOCUMENTS
	${StrReplace} $REPLACEVAR_DBS_DOCUMENTS / \\ $REPLACEVAR_FS_DOCUMENTS
	${StrReplace} $REPLACEVAR_FS_PORTABLEAPPSDOCUMENTSDIRECTORY \ / $PORTABLEAPPSDOCUMENTSDIRECTORY
	${StrReplace} $REPLACEVAR_DBS_PORTABLEAPPSDOCUMENTSDIRECTORY / \\ $REPLACEVAR_FS_PORTABLEAPPSDOCUMENTSDIRECTORY
	${StrReplace} $REPLACEVAR_FS_PORTABLEAPPSPICTURESDIRECTORY \ / $PORTABLEAPPSPICTURESDIRECTORY
	${StrReplace} $REPLACEVAR_DBS_PORTABLEAPPSPICTURESDIRECTORY / \\ $REPLACEVAR_FS_PORTABLEAPPSPICTURESDIRECTORY
	${StrReplace} $REPLACEVAR_FS_PORTABLEAPPSMUSICDIRECTORY \ / $PORTABLEAPPSMUSICDIRECTORY
	${StrReplace} $REPLACEVAR_DBS_PORTABLEAPPSMUSICDIRECTORY / \\ $REPLACEVAR_FS_PORTABLEAPPSMUSICDIRECTORY
	${StrReplace} $REPLACEVAR_FS_PORTABLEAPPSVIDEOSDIRECTORY \ / $PORTABLEAPPSVIDEOSDIRECTORY
	${StrReplace} $REPLACEVAR_DBS_PORTABLEAPPSVIDEOSDIRECTORY / \\ $REPLACEVAR_FS_PORTABLEAPPSVIDEOSDIRECTORY
	${StrReplace} $REPLACEVAR_FS_PORTABLEAPPSDIRECTORY \ / $PORTABLEAPPSDIRECTORY
	${StrReplace} $REPLACEVAR_DBS_PORTABLEAPPSDIRECTORY / \\ $REPLACEVAR_FS_PORTABLEAPPSDIRECTORY
!macroend

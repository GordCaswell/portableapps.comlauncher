${SegmentFile}

; Macros {{{1
!macro ParseLocations_SlashType VAR SLASHTYPE VARIABLEAPPENDAGE ;{{{2
	${WordReplace} "${VAR}" "%${SLASHTYPE}APPDIR%" "$${VARIABLEAPPENDAGE}APPDIRECTORY" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}DATADIR%" "$${VARIABLEAPPENDAGE}DATADIRECTORY" + "${VAR}"
	${If} $JavaMode == find
	${OrIf} $JavaMode == require
		${WordReplace} "${VAR}" "%${SLASHTYPE}JAVADIR%" "$${VARIABLEAPPENDAGE}JAVADIRECTORY" + "${VAR}"
	${EndIf}
	${WordReplace} "${VAR}" "%${SLASHTYPE}ALLUSERSPROFILE%" "$${VARIABLEAPPENDAGE}ALLUSERSPROFILE" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}LOCALAPPDATA%" "$${VARIABLEAPPENDAGE}LOCALAPPDATA" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}APPDATA%" "$${VARIABLEAPPENDAGE}APPDATA" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}DOCUMENTS%" "$${VARIABLEAPPENDAGE}DOCUMENTS" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}TEMP%" "$${VARIABLEAPPENDAGE}TEMPDIRECTORY" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSDOCUMENTSDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSDOCUMENTSDIRECTORY" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSPICTURESDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSPICTURESDIRECTORY" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSMUSICDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSMUSICDIRECTORY" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSVIDEOSDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSVIDEOSDIRECTORY" + "${VAR}"
	${WordReplace} "${VAR}" "%${SLASHTYPE}PORTABLEAPPSDIR%" "$${VARIABLEAPPENDAGE}PORTABLEAPPSDIRECTORY" + "${VAR}"
!macroend

!macro ParseLocations VAR ;{{{2
	${DebugMsg} "Before location parsing, $${VAR} = `${VAR}`"
	;===Paths {{{3
		${WordReplace} "${VAR}" %DRIVE% $CurrentDrive + "${VAR}"
		!insertmacro ParseLocations_SlashType "${VAR}" "" ""
		!insertmacro ParseLocations_SlashType "${VAR}" /  REPLACEVAR_FS_
		!insertmacro ParseLocations_SlashType "${VAR}" \\ REPLACEVAR_DBS_
		${If} $JavaMode == find
		${OrIf} $JavaMode == require
			!insertmacro ParseLocations_SlashType "${VAR}" java.util.prefs: REPLACEVAR_JUP_
		${EndIf}

	;===Languages {{{3
		${WordReplace} "${VAR}" %LANGCODE% $PORTABLEAPPSLANGUAGECODE + "${VAR}"
		${WordReplace} "${VAR}" %LANGCODE2% $PORTABLEAPPSLOCALECODE2 + "${VAR}"
		${WordReplace} "${VAR}" %LANGCODE3% $PORTABLEAPPSLOCALECODE3 + "${VAR}"
		${WordReplace} "${VAR}" %LANGGLIBC% $PORTABLEAPPSLOCALEGLIBC + "${VAR}"
		${WordReplace} "${VAR}" %LANGID% $PORTABLEAPPSLOCALEID + "${VAR}"
		${WordReplace} "${VAR}" %LANGWINNAME% $PORTABLEAPPSLOCALEWINNAME + "${VAR}"
	${DebugMsg} "After location parsing, $${VAR} = `${VAR}`"
!macroend
!define ParseLocations "!insertmacro ParseLocations"

; Variables {{{1
Var APPDIRECTORY
Var DATADIRECTORY
Var ALLUSERSPROFILE
Var TEMPDIRECTORY
Var PORTABLEAPPSDOCUMENTSDIRECTORY
Var PORTABLEAPPSPICTURESDIRECTORY
Var PORTABLEAPPSMUSICDIRECTORY
Var PORTABLEAPPSVIDEOSDIRECTORY
Var PORTABLEAPPSDIRECTORY

!macro Var_ReplaceVar _VAR
Var REPLACEVAR_FS_${_VAR}
Var REPLACEVAR_DBS_${_VAR}
Var REPLACEVAR_JUP_${_VAR}
!macroend

!insertmacro Var_ReplaceVar APPDIRECTORY
!insertmacro Var_ReplaceVar DATADIRECTORY
!insertmacro Var_ReplaceVar ALLUSERSPROFILE
!insertmacro Var_ReplaceVar LOCALAPPDATA
!insertmacro Var_ReplaceVar APPDATA
!insertmacro Var_ReplaceVar DOCUMENTS
!insertmacro Var_ReplaceVar TEMPDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSDOCUMENTSDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSPICTURESDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSMUSICDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSVIDEOSDIRECTORY
!insertmacro Var_ReplaceVar PORTABLEAPPSDIRECTORY

Var PORTABLEAPPSLANGUAGECODE
Var PORTABLEAPPSLOCALECODE2
Var PORTABLEAPPSLOCALECODE3
Var PORTABLEAPPSLOCALEGLIBC
Var PORTABLEAPPSLOCALEID
Var PORTABLEAPPSLOCALEWINNAME

; Segments {{{1
${SegmentInit}
	;=== Initialise variables
	${GetParent} $EXEDIR $PORTABLEAPPSDIRECTORY

	ReadEnvStr $PORTABLEAPPSDOCUMENTSDIRECTORY PortableApps.comDocuments
	${IfNotThen} ${FileExists} $PORTABLEAPPSDOCUMENTSDIRECTORY ${|} StrCpy $PORTABLEAPPSDOCUMENTSDIRECTORY $CurrentDrive\Documents ${|}

	ReadEnvStr $PORTABLEAPPSPICTURESDIRECTORY PortableApps.comPictures
	${IfNotThen} ${FileExists} $PORTABLEAPPSPICTURESDIRECTORY ${|} StrCpy $PORTABLEAPPSPICTURESDIRECTORY $PORTABLEAPPSDOCUMENTSDIRECTORY\Pictures ${|}

	ReadEnvStr $PORTABLEAPPSMUSICDIRECTORY PortableApps.comMusic
	${IfNotThen} ${FileExists} $PORTABLEAPPSMUSICDIRECTORY ${|} StrCpy $PORTABLEAPPSMUSICDIRECTORY $PORTABLEAPPSDOCUMENTSDIRECTORY\Music ${|}

	ReadEnvStr $PORTABLEAPPSVIDEOSDIRECTORY PortableApps.comVideos
	${IfNotThen} ${FileExists} $PORTABLEAPPSVIDEOSDIRECTORY ${|} StrCpy $PORTABLEAPPSVIDEOSDIRECTORY $PORTABLEAPPSDOCUMENTSDIRECTORY\Videos ${|}

	ReadEnvStr $PORTABLEAPPSLANGUAGECODE PortableApps.comLanguageCode
	${IfThen} $PORTABLEAPPSLANGUAGECODE == "" ${|} StrCpy $PORTABLEAPPSLANGUAGECODE en-us ${|}
	ReadEnvStr $PORTABLEAPPSLOCALECODE2 PortableApps.comLocaleCode2
	${IfThen} $PORTABLEAPPSLOCALECODE2 == "" ${|} StrCpy $PORTABLEAPPSLOCALECODE2 en ${|}
	ReadEnvStr $PORTABLEAPPSLOCALECODE3 PortableApps.comLocaleCode3
	${IfThen} $PORTABLEAPPSLOCALECODE3 == "" ${|} StrCpy $PORTABLEAPPSLOCALECODE3 eng ${|}
	ReadEnvStr $PORTABLEAPPSLOCALEGLIBC PortableApps.comLocaleglibc
	${IfThen} $PORTABLEAPPSLOCALEGLIBC == "" ${|} StrCpy $PORTABLEAPPSLOCALEGLIBC en_US ${|}
	ReadEnvStr $PORTABLEAPPSLOCALEID PortableApps.comLocaleID
	${IfThen} $PORTABLEAPPSLOCALEID == "" ${|} StrCpy $PORTABLEAPPSLOCALEID 1033 ${|}
	ReadEnvStr $PORTABLEAPPSLOCALEWINNAME PortableApps.comLocaleWinName
	${IfThen} $PORTABLEAPPSLOCALEWINNAME == "" ${|} StrCpy $PORTABLEAPPSLOCALEWINNAME LANG_ENGLISH ${|}

	ReadEnvStr $ALLUSERSPROFILE ALLUSERSPROFILE

	;=== Make forward slash and double backslash versions
	${WordReplace} $ALLUSERSPROFILE                \ /  + $REPLACEVAR_FS_ALLUSERSPROFILE
	${WordReplace} $ALLUSERSPROFILE                \ \\ + $REPLACEVAR_DBS_ALLUSERSPROFILE
	${WordReplace} $LOCALAPPDATA                   \ /  + $REPLACEVAR_FS_LOCALAPPDATA
	${WordReplace} $LOCALAPPDATA                   \ \\ + $REPLACEVAR_DBS_LOCALAPPDATA
	${WordReplace} $APPDATA                        \ /  + $REPLACEVAR_FS_APPDATA
	${WordReplace} $APPDATA                        \ \\ + $REPLACEVAR_DBS_APPDATA
	${WordReplace} $DOCUMENTS                      \ /  + $REPLACEVAR_FS_DOCUMENTS
	${WordReplace} $DOCUMENTS                      \ \\ + $REPLACEVAR_DBS_DOCUMENTS
	${WordReplace} $PORTABLEAPPSDOCUMENTSDIRECTORY \ /  + $REPLACEVAR_FS_PORTABLEAPPSDOCUMENTSDIRECTORY
	${WordReplace} $PORTABLEAPPSDOCUMENTSDIRECTORY \ \\ + $REPLACEVAR_DBS_PORTABLEAPPSDOCUMENTSDIRECTORY
	${WordReplace} $PORTABLEAPPSPICTURESDIRECTORY  \ /  + $REPLACEVAR_FS_PORTABLEAPPSPICTURESDIRECTORY
	${WordReplace} $PORTABLEAPPSPICTURESDIRECTORY  \ \\ + $REPLACEVAR_DBS_PORTABLEAPPSPICTURESDIRECTORY
	${WordReplace} $PORTABLEAPPSMUSICDIRECTORY     \ /  + $REPLACEVAR_FS_PORTABLEAPPSMUSICDIRECTORY
	${WordReplace} $PORTABLEAPPSMUSICDIRECTORY     \ \\ + $REPLACEVAR_DBS_PORTABLEAPPSMUSICDIRECTORY
	${WordReplace} $PORTABLEAPPSVIDEOSDIRECTORY    \ /  + $REPLACEVAR_FS_PORTABLEAPPSVIDEOSDIRECTORY
	${WordReplace} $PORTABLEAPPSVIDEOSDIRECTORY    \ \\ + $REPLACEVAR_DBS_PORTABLEAPPSVIDEOSDIRECTORY
	${WordReplace} $PORTABLEAPPSDIRECTORY          \ /  + $REPLACEVAR_FS_PORTABLEAPPSDIRECTORY
	${WordReplace} $PORTABLEAPPSDIRECTORY          \ \\ + $REPLACEVAR_DBS_PORTABLEAPPSDIRECTORY
!macroend
